import org.jooq.codegen.GenerationTool
import org.gradle.internal.os.OperatingSystem;

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.jooq:jooq-codegen:3.18.7'
        classpath 'org.xerial:sqlite-jdbc:3.43.2.1'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.onebyte-llc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

version = passedAppVersion
application {
    mainModule = 'com.onebyte_llc.imageviewer'
    mainClass = 'com.onebyte_llc.imageviewer.MainApplication'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml']
}


def runtimeJvmArgs = [] // '-Xmx500m', '-Xms500m'
run {
    jvmArgs = runtimeJvmArgs
}

dependencies {
    implementation("org.xerial:sqlite-jdbc:3.43.2.1")
    implementation("org.jooq:jooq:3.18.7")
    implementation("org.apache.commons:commons-imaging:1.0-alpha3")
    implementation("net.java.dev.jna:jna:5.14.0")

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'ImageViewer'
    }
    jpackage {
        imageName = 'ImageViewer'
        jvmArgs = runtimeJvmArgs
        installerType = 'app-image'

        def iconPath = 'src/main/resources/image/branding/'
        if (OperatingSystem.current().isMacOsX()) {
            iconPath += 'app-icon.icns'
        } else if (OperatingSystem.current().isWindows()) {
            iconPath += 'app-icon.ico'
        } else {
            iconPath += 'app-icon.png'
        }
        icon = iconPath
    }
}

jlinkZip {
    group = 'distribution'
}


import org.jooq.meta.jaxb.*

tasks.register('generateJooqBindings') {
    doLast {
        GenerationTool.generate(new Configuration()
                .withJdbc(new Jdbc()
                        .withDriver('org.sqlite.JDBC')
                        .withUrl('jdbc:sqlite:/Users/shorton/Library/Application Support/com.onebyte_llc.imageviewer/imageview.db')
                        .withUser('')
                        .withPassword(''))
                .withGenerator(new Generator()
                        .withDatabase(new Database())
                        .withGenerate(new Generate()
                                .withPojos(true)
                                .withDaos(true))
                        .withTarget(new Target()
                                .withPackageName('com.onebyte_llc.imageviewer.backend.db.jooq')
                                .withDirectory(buildDir.absolutePath + '/../src/main/java'))))
    }
}